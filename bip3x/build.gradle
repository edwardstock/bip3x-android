apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = "1.0.0"
group = "com.edwardstock.android"

ext {
    buildArtifactName = "bip3x"
    buildArtifactVersion = version
    buildArtifactGroup = group

    pomName = "bip3x"
    pomUrl = "https://github.com/edwardstock/bip3x"
    pomScm = {
        connection = "scm:git:git://github.com/edwardstock/bip3x.git"
        url = pomUrl
    }
    pomInceptionYear = "2018"
    pomContributors = {}
    pomDescription = "BIP39 mnemonic for android"
    pomDevelopers = {
        developer {
            id = "edwardstock"
            name = "Eduard Maximovich"
            email = "edwatd.vstock@gmail.com"
            roles = ["owner"]
            timezone = "Europe/Moscow"
        }
    }
    pomLicenses = {
        license {
            name = "MIT"
            url = "https://github.com/edwardstock/bip3x/LICENSE"
            distribution = "repo"
        }
    }
}

def libPath = System.getenv('BIP39_LIBS')
if(hasProperty("nativeLibPath")) {
    libPath = getProperty("nativeLibPath")
}
println "Native lib path: ${libPath}"

android {
    compileSdkVersion maxSdk
    buildToolsVersion buildToolsVers

    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion maxSdk
        versionCode 1
        versionName version

        externalNativeBuild {
            cmake {
                arguments = [
                        '-DENABLE_BIP39_TESTS=Off',
                        '-DENABLE_BIP39_C=Off',
                        '-DENABLE_BIP39_SHARED=On',
                        '-DENABLE_BIP39_JNI=On',
                        '-DENABLE_BIP39_CONAN=Off',
                        '-DENABLE_TOOLBOX_TEST=Off',
                        '-DENABLE_TOOLBOX_CONAN=Off',
                        '-DENABLE_SHARED=On',
                ]
                cppFlags "-std=c++14"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    sourceSets {
        main {
            jni.srcDirs = [
                    'src/main/cpp/bip3x/src/java'
            ]
            jniLibs.srcDirs = ['libs']
            java.srcDirs = [
                    'src/main/java',
                    'src/main/cpp/bip3x/src/java'
            ]
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
            version '3.10.2'
        }
    }

    testOptions {
        unitTests {
            all {
                jvmArgs "-Djava.library.path=${libPath}"
            }
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // annotations
    compileOnly 'javax.annotation:jsr250-api:1.0'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2@jar'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

apply from: '../scripts/maven_publish.gradle'
